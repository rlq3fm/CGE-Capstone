$title  electricity sector in China Hybrid Energy and Economics Research (CHEER) Model

$Ontext
1) Read electricity data
2) calibration of benchmark
3) covert from value unit (Yuan) to physical unit (GWh)
4) calibration of substitution elasticities between fixed factor and other inputs for elecpower

Author:Yaqian Mu, Tsinghua University (muyaqian00@163.com)
UpDate:    12-2016
$offtext
*----------------------------------------------*

*----------------------------------------------*
*==Read electricity data
*----------------------------------------------*
Parameter  sam_elec(*,*) input data of sub-electricity sectors
           laborelec0(*,*) employment quantity of sub-electricity sectors by labor types in  thousand persons
           laborelec_v0(*,*) labor compensation of sub-electricity sectors by labor types in  thousand persons
;
$GDXIN     %DataPath%\electricity_%ExpPath%_%datagg%.gdx
$LOAD      sam_elec laborelec0 laborelec_v0

*----------------------------------------------*
*==calibration of benchmark
*----------------------------------------------*

*=== transfer unit from 10 thousand yuan to billion yuan
            sam_elec(i,sub_elec)=sam_elec(i,sub_elec)/100000;
            sam_elec(f,sub_elec)=sam_elec(f,sub_elec)/100000;
            sam_elec('tax',sub_elec)=sam_elec('tax',sub_elec)/100000;

*=== transfer unit from 10 thousand person to thousand people
            laborelec0(lmo,sub_elec)=laborelec0(lmo,sub_elec)*10;
            laborelec0(lm,sub_elec)=sum(lmo$(maplmo(lmo,lm)),laborelec0(lmo,sub_elec));

DISPLAY     sam_elec,laborelec0;

parameter DJ(gen) direct employment factors from (Cai 2011)  jobs per GW?1?71?1?77?1?71?1?77
      / coal_power        1322
      Oil_Power           1790
      Gas_Power           770
      hydro               1953
      nuclear             615
      wind                378
      biomass             3222
      solar               497
      /;

parameter GW(gen) intalled capacity
      / coal  753.82
        oil 3.01
        gas 37.17
        nuclear 12.57
        hydro 249.47
        wind  61.42
        solar 3.41
        biomass 7.69
      /;

parameter ltqelec0
          lqelec0;
          ltqelec0(gen)  = GW(gen)*DJ(gen)/1000
          ltqelec0("T_D") = sum((lm,sub_elec),laborelec0(lm,sub_elec))-sum(gen,ltqelec0(gen));

          lqelec0(lm,sub_elec)      = ltqelec0(sub_elec)*laborelec0(lm,sub_elec)/sum(lmm,laborelec0(lm,sub_elec));
$stop  


parameter
         ffshr           Fraction of electric sector capital as fixed factor
         lelec0          Labor in electricity generation
         kelec0          Capital in electricity generation
         ffelec0         Fixed factor in electricity generation
         intelec0        Intermediate input to electricity generation
         taxelec0        Tax cost in electricity generation
         subelec0        Total subsidy in billion yuan
         outputelec0     Output of electricity generation
         Toutputelec0    Total Output of electricity generation
         subelec_b       Benchmark for electricity subsidy
         ;

*// block for markup factor; do not work if emkup=1;
parameter
          emkup           electricity markup
          emkup0          base year electricity markup;

          emkup(sub_elec)       =      1;
*         emkup("wind")         =      1.3;
*         emkup("solar")        =      2.5;
*         emkup("Biomass")      =      1.8;
          emkup0(sub_elec)      =      emkup(sub_elec);

          lelec0(sub_elec)      =       sam_elec("labor",sub_elec)/emkup(sub_elec);
          kelec0(sub_elec)      =       sam_elec("capital",sub_elec)/emkup(sub_elec);
          intelec0(i,sub_elec)  =       sam_elec(i,sub_elec)/emkup(sub_elec);
          taxelec0(sub_elec)    =       sam_elec("tax",sub_elec);

*==fixed factors for elecpower
*==to be updata
parameter theta_elec(sub_elec)     imputed fixed factor share of capital      from Sue Wing 2006
          p_ff            proportion of fix-factor£»

table ff_data(*,*) cost structure from Sue Wing
                 hydro      nuclear    Wind       Solar      Biomass
labor            24         13         17         7          19
capital          56         60         64         73         59
ff               19         27         20         20         22
;
            theta_elec(sub_elec)    =0;
            theta_elec(sub_elec)$ff_data("ff",sub_elec)
                                    = ff_data("ff",sub_elec)/(ff_data("ff",sub_elec)+ff_data("capital",sub_elec));
*// split fixed factors from capital
            ffelec0(sub_elec)       =    0;
            ffelec0(sub_elec)       =    theta_elec(sub_elec)*kelec0(sub_elec);
            kelec0(sub_elec)        =   (1-theta_elec(sub_elec))*kelec0(sub_elec);

display     taxelec0,ffelec0;

*----------------------------------------------*
*// bleck for tax and subsidy
*----------------------------------------------*
parameter   q_gen(*)    data for power generation by technology in GWh from Stats
            subsidy(*)  total value of subsidy by technologies  in  10 thousand yuan
            P_elec(*)   Feed in tariff Price Yuan per kwh Source Qi 2014 Energy Policy;
$LOAD       q_gen subsidy P_elec
$GDXIN

*// convert subsidy from 10 thousand yuan to billion yuan
            subelec0(sub_elec) =    subsidy(sub_elec)/100000;

*// Feed in tariff and electricity consumption fee data
*// allocate ecf to aggregated electricity sector
parameter   FIT(sub_elec)   Feed in tariff Price Yuan per kwh
            ecf0            initinal electricity comsumption fee to cover the renewable subsidy ;

            FIT(sub_elec)      =    P_elec(sub_elec);
            ecf0               =    sum(sub_elec,subelec0(sub_elec));
*           ecf0=0;

*// split ecf from tax of sub_elec
            taxelec0(sub_elec) =  taxelec0(sub_elec)
                                  -ecf0*q_gen(sub_elec)/sum(sub_elecc,q_gen(sub_elecc));
display     subelec0, taxelec0;

*// update the gross tax = net tax + subsidy
            taxelec0(sub_elec) =   taxelec0(sub_elec)
                                   + subelec0(sub_elec)  ;

display     subelec0, taxelec0;

*//update outputelec0
            outputelec0(sub_elec)   =   emkup(sub_elec)*(lelec0(sub_elec)+kelec0(sub_elec)
                                        +ffelec0(sub_elec)+sum(i,intelec0(i,sub_elec)))
                                        +(taxelec0(sub_elec)-subelec0(sub_elec));

            Toutputelec0            =   sum(sub_elec,outputelec0(sub_elec));


*//convert tax value to tax rate
            taxelec0(sub_elec)$outputelec0(sub_elec)    =   taxelec0(sub_elec)/outputelec0(sub_elec);
            subelec0(sub_elec)$outputelec0(sub_elec)    =   subelec0(sub_elec)/outputelec0(sub_elec);
            ecf0               =    ecf0/output0("elec");


            subelec_b(sub_elec)     =   subelec0(sub_elec);
            p_ff(sub_elec)          =   emkup(sub_elec)*ffelec0(sub_elec)/((1-taxelec0(sub_elec))*outputelec0(sub_elec));

*// key debug points=======
*emarkup

            fact('capital')         =   fact('capital')-sum(sub_elec,ffelec0(sub_elec)*emkup(sub_elec));

display lelec0,kelec0,intelec0,outputelec0,taxelec0,subelec0,tx0,emission0,p_ff,Toutputelec0;

*----------------------------------------------*
*// covert from value unit (Yuan) to physical unit (GWh)
*----------------------------------------------*
*convert q from MWh to GWh
            q_gen(sub_elec)    =    q_gen(sub_elec)/1000;

display     q_gen;

*// transfer elecoutput from value to physical in TWH
parameter   costelec0(sub_elec) unit generation cost by technomogy in billion yuan per TWH;

            costelec0(sub_elec)     =   outputelec0(sub_elec)/q_gen(sub_elec)*1000;

            outputelec0(sub_elec)   =   q_gen(sub_elec)/1000;

*// set emission for electricity sector
parameter   emissionelec0(*,*,*,*)  electricity emission by source ;

            emissionelec0('co2','e',fe,sub_elec)    =   ccoef_p(fe)*intelec0(fe,sub_elec)*(1-r_feed(fe,"elec"));

*//EPPA 6 elecpower substitution elasticity between fixed factor and other inputs
            esub(sub_elec,"ff")     =   0.2;
            esub("nuclear","ff")    =   0.6*p_ff("nuclear")/(1-p_ff("nuclear"));
            esub("hydro","ff")      =   0.5*p_ff("hydro")/(1-p_ff("hydro"));
            esub("wind","ff")       =   0.25;

display     costelec0,outputelec0,esub,emissionelec0, esub;


parameter check1,check2;
check1  =        output0("elec")*ecf0 ;
check2  =        sum(sub_elec, outputelec0(sub_elec)*costelec0(sub_elec)*subelec0(sub_elec));

*$stop

*=======block for electricity investment mainly based on Dai 2016
Table InvShr(i,*)  Investment demand share for non-fossil power generation and other sectors  % from Dai 2016
                 Other    Hydro    Nuclear  Wind     Biomass   Solar
Agri             2
MetalPr          2                          5        3         3
TransEqp         7        2                 5        10
GenEqp           24       28       55       38       40        40
ElecEqp          5        7        15       5        8         12
OthMfg           1
Constr           49       35       25       15       25        13
OthTran          2        8                 12       5         8
RD               0        12       5        8        4         10
Service          8        8                 12       5         14
;

Table ConEff(*,*)  2012 power generation conversion efficiency
        coal_Power  oil_Power gas_Power nuclear hydro wind  solar biomass
GW2TWh  4.92        1.79      2.94      7.82    3.43  1.68  1.06  4.11
TWh2GW  0.20        0.56      0.34      0.13    0.29  0.60  0.95  0.24
;

parameter InvCost(*)  Investment cost for power generation from Dai 2016 in billion Yuan per GW
/Coal_Power       4.38
Oil_Power         2.85
Gas_Power         2.85
Hydro       6
Nuclear     10
Wind        9
Biomass     20
Solar       20
/
;

parameter IniCap0   initinal capacity of electricity in base year in GW
 /coal_Power    753.82
  oil_Power     3.01
  gas_Power     37.17
  nuclear 12.57
  hydro   249.47
  wind    61.42
  solar   3.41
  biomass 7.69
  /
;

parameter IniGen0   initinal generation of electricity in base year in GW
 /coal_Power  3710.4
  oil_Power 5.4
  gas_Power 109.2
  nuclear 98.3
  hydro 855.6
  wind  103
  solar 3.6
  biomass 31.6
  /
;

parameter elecinv0  initinal value for electricity capacity investment
          ;

parameter NewCap0   new capacity of electricity in base year in GW
 /coal_Power    44.53
  oil_Power     -0.27
  gas_Power     3.02
  nuclear       0
  hydro         16.49
  wind          15.19
  solar         1.29
  biomass       2.1
  /
;

*// ignore the tiny negetive change of oil-fired capacity
  NewCap0(sub_elec)$(NewCap0(sub_elec) le 0) = 0.001;
  NewCap0(TD)=0;

  elecinv0(i,sub_elec)  = NewCap0(sub_elec)*InvCost(sub_elec)*InvShr(i,sub_elec)/100;
  elecinv0(i,ffe)       = NewCap0(ffe)*InvCost(ffe)*InvShr(i,'other')/100;

*//update investment data to keep balance; split electricity investment from aggregate investment
  inv0(i)$switch_inv = inv0(i)-sum(sub_elec,elecinv0(i,sub_elec));

*//update capital data to keep balance; split capital use in electricity sector from aggregate capital
  fact("capital")$switch_inv = fact("capital")-sum(gen,kelec0(gen));
